buildscript {
    repositories {
        jcenter()
    }

}

plugins {
//    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

apply plugin: 'java-library'
apply plugin: 'signing'

ext {
    springBaseVersion = '5.1.0.RELEASE'
    jacksonVersion = "2.8.11"
    isRelease = false
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled true
        csv.enabled false
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [""])
        })
    }
}

group 'gr.mmichailidis'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/milestone' }
}

test {
    maxParallelForks = 1
}

dependencies {
    implementation group: 'io.vavr', name: 'vavr', version: '1.0.0-alpha-3'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.1.6.RELEASE'
    implementation group: 'org.json', name: 'json', version: '20180813'

    implementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'

    implementation('org.apache.qpid:qpid-broker:7.0.6') {
        force = true
        exclude(module: "qpid-broker-plugins-logging-logback")
        exclude(module: "com.sleepycat:je:7.4.5")
        exclude group: "ch.qos.logback", module: "logback-classic"
        exclude group: "ch.qos.logback", module: "logback-cor`e"
    }

    implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-common', version: '9.4.3.v20170317'
    implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-server', version: '9.4.3.v20170317'
    implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-api', version: '9.4.3.v20170317'
    implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-servlet', version: '9.4.3.v20170317'
    implementation group: 'org.eclipse.jetty', name: 'jetty-io', version: '9.4.3.v20170317'
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.3.v20170317'
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.3.v20170317'
    implementation group: 'org.eclipse.jetty', name: 'jetty-http', version: '9.4.3.v20170317'
    implementation group: 'org.eclipse.jetty', name: 'jetty-util', version: '9.4.3.v20170317'
    implementation group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '9.4.3.v20170317'

    implementation group: 'com.sleepycat', name: 'je', version: '18.1.11'
    implementation group: 'io.projectreactor.rabbitmq', name: 'reactor-rabbitmq', version: '1.0.0.RELEASE'
    testImplementation group: 'io.projectreactor', name: 'reactor-test', version: '3.2.2.RELEASE'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.8'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'gr.mmichailidis'
            artifactId 'amqprunner'
            version '1.0.0'

            from components.java

            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            if (isRelease) {
                project.tasks.signArchives.signatureFiles.each {
                    artifact(it) {
                        def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                        if (matcher.find()) {
                            classifier = matcher.group(1)
                        } else {
                            classifier = null
                        }
                        extension = 'jar.asc'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

publish {
    isRelease = true
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'In-Memory AMQP broker'
            name 'AmqpRunner'
            url 'https://github.com/mmichailidis/AmqpRunner'
            organization {
                name 'gr.mmichailidis'
                url 'https://github.com/mmichailidis'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/mmichailidis/AmqpRunner/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/mmichailidis/AmqpRunner/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/mmichailidis/AmqpRunner/'
                connection 'git@github.com:mmichailidis/AmqpRunner.git'
                developerConnection 'git@github.com:mmichailidis/AmqpRunner.git'
            }
            developers {
                developer {
                    name 'MMichailidis'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}
